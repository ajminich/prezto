#
# A monochrome theme that displays basic information.
#
# Authors:
#   Brian Tse <briankftse@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/zLZNK.png
#

function prompt_aj_precmd {
  git-info
}

function prompt_aj_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_aj_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%b'
  zstyle ':prezto:module:git:info:remote' format '%R'
  zstyle ':prezto:module:git:info:action' format '%s'
  zstyle ':prezto:module:git:info:ahead' format ' %F{green}%A⬆%f'
  zstyle ':prezto:module:git:info:behind' format ' %F{red}%B⬇%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{cyan}($(coalesce "%s" "%R" "%b" "%p")${git_info[rprompt]}%s%F{cyan})%f' \
    'rprompt' '%A%B%C%D'

  # Set the preamble: time username@hostname
  preamble='%F{white}%* %n@%m%f'

  # Set the directory color: green if writeable, yellow if not.
  [[ -w $PWD ]] && currentdir='%F{green}%~%f' || currentdir='%F{yellow}%~%f'

  # Define prompts.
  PROMPT='${preamble}:${currentdir}${(e)git_info[prompt]}
%F{white}>%f '
  RPROMPT=''
}

prompt_aj_setup "$@"
